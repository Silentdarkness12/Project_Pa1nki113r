ACTOR GTechPillar
{
  Health 10
  Mass 99999
  Radius 12
  Scale 1.2
  Height 128
  DeathHeight 128
  +SOLID
  +SHOOTABLE
  +NOBLOOD
  +DONTGIB
  +NOICEDEATH
  +NODAMAGETHRUST
  +FORCEYBILLBOARD
  States
  {
  Spawn:
     GTEC A -1
     Loop
  Death:
     GTEC B 1
     TNT1 A 0 A_PlaySound("PillarGlassBreak")
	 TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("PillarGlassShard",0,0,48,random(-2,2),random(-2,2),random(3,9),random(0,359),32)
	 TNT1 AAAA 0 A_SpawnItemEx("PillarGlassShard",0,0,68,random(-2,2),random(-2,2),random(3,9),random(0,359),32)	
     GTEC B -1
	 Stop
  }
}

ACTOR PillarGlassShard
{
  Radius 1
  Height 1
  Speed 15
  PROJECTILE
  -NOGRAVITY
  +THRUACTORS
  +MOVEWITHSECTOR
  Gravity 0.6
  RenderStyle Add
  Alpha 0.6
  Scale 1.3
  DeathSound "GlassShardImpact"
  States
  {
  Spawn:
     TNT1 A 0
     TNT1 A 0 A_Jump(255,"Standard","Small","Tiny","XLarge") //Randomly selects a glass shard size.
  Standard:	
     PGSD ABCD 1
	 Loop
  Small:	
     TNT1 A 0 A_SetScale(0.8)
     PGSD ABCD 1
	 Loop	
  Tiny:	
     TNT1 A 0 A_SetScale(0.4)
     PGSD ABCD 1
	 Loop
  XLarge:	
     TNT1 A 0 A_SetScale(1.6)
     PGSD ABCD 1
	 Loop	 
  Death:
     TNT1 A 0 A_SetTranslucent(0.3)
     PGSD E 125
     Stop
  }
}